/*
 * Copyright 2021 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.9"
}

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

tasks.register('jacocoDevDebugUnitTestReport', JacocoReport) {
    dependsOn 'testDevDebugUnitTest'
    group = "Reporting"
    description = "Run tests and generate coverage reports"
    reports {
        csv.required = false
        xml.required = true
        html.required = true
    }
    classDirectories.from = fileTree(
        dir: 'build/intermediates/classes/devDebug/transformDevDebugClassesWithAsm/dirs/com/google/android/ground',
        excludes: [
            '**/databinding/*',
            '**/local/room/dao/*',
            '**/*_Factory*',
            '**/*Hilt*',
            '**/*Args*',
            '**/*Directions*',
            '**/LocalDatabase_Impl*',
            '**/*Module*',
        ]
    )
    sourceDirectories.from = files('src/main/java/com/google/android/ground')
    executionData.from = files('build/jacoco/testDevDebugUnitTest.exec')
}
