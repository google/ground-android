/*
 * Copyright 2019 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Spotbugs plugin configuration.
 *
 * It finds bugs in Java programs. It looks for instances of “bug patterns” — code instances that
 * are likely to be errors.
 * GitHub link: https://github.com/spotbugs/spotbugs
 *
 * Runs automatically with every build on GoogleCloudBuild.
 *
 * For manual run,
 * $ ./gradlew spotbugs
 *
 * Report (only html format. Both can't be generated) are stored under: gnd/build/reports/spotbugs/
 */

import com.github.spotbugs.SpotBugsTask

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.4'
    }
}

apply plugin: 'com.github.spotbugs'

spotbugs {
    toolVersion = '4.0.0-beta4'
}

def configDir = "${project.rootDir}/config/spotbugs"
def reportsDir = "${project.buildDir}/reports"

tasks.register("spotbugs", SpotBugsTask) {
    ignoreFailures false // Fail early
    effort = 'max'

    reportLevel = 'low'

    excludeFilter = new File("$configDir/spotbugs-filter.xml")
    classes = fileTree("build/intermediates/javac/")

    source = 'src'

    classpath = files()
    pluginClasspath = project.configurations.spotbugsPlugins
    spotbugsClasspath = buildscript.configurations.classpath

    setMaxHeapSize('256m')

    reports {
        html {
            enabled true
            setDestination new File("$reportsDir/spotbugs/spotbugs.html")
        }
        xml {
            enabled false
            setDestination new File("$reportsDir/spotbugs/spotbugs.xml")
        }
    }
}