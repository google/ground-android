/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file. Configuration specified here is inherited by all sub-projects/modules.

buildscript {
    ext {
        // As of Gradle 8.5, Kotlin plugin only tested up to 1.9.20.
        kotlinVersion = "1.9.20"
        hiltVersion = "2.50"
        navigationVersion = "2.5.3"
        roomVersion = "2.6.1"
    }
    repositories {
        google()
        mavenLocal()
        maven { url "https://maven.google.com" }
        maven { url "https://github.com/ben-manes/gradle-versions-plugin/raw/mvnrepo" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        classpath 'com.android.tools.build:gradle:8.2.0'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath "com.pascalwelsch.gitversioner:gitversioner:0.5.0"

        // Performance Monitoring plugin: https://firebase.google.com/docs/perf-mon
        classpath 'com.google.firebase:perf-plugin:1.4.2'

        // Crashlytics plugin
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.5'

        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.36.0"
    id "org.jetbrains.kotlin.android" version "$kotlinVersion" apply false
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlinVersion"
    id "com.ncorti.ktfmt.gradle" version "0.11.0"
    id "com.google.dagger.hilt.android" version "$hiltVersion" apply false
    id "io.gitlab.arturbosch.detekt" version "1.23.0"
}

allprojects {

    repositories {
        google() // For github repos.
        mavenLocal()
        maven { url "https://maven.google.com" }
        maven { url "https://jitpack.io" }
        maven { url "https://repo.osgeo.org/repository/release/" } // GeoTools
        mavenCentral()
    }
}

subprojects {
    apply plugin: "com.ncorti.ktfmt.gradle" // Version should be inherited from parent

    // https://github.com/cortinico/ktfmt-gradle
    ktfmt {
        googleStyle()
    }
}

detekt {
    toolVersion = "1.23.0"

    source = files("ground/src", "sharedTest/src")
    allRules = true
    config = files("${project.rootDir}/config/detekt/detekt.yml")
    baseline = file("${project.rootDir}/config/detekt/baseline.xml")
    buildUponDefaultConfig = true
    ignoreFailures = false

    basePath = projectDir
}

task checkCode(type: GradleBuild) {
    tasks = ['checkstyle', 'lintLocalDebug', 'ktfmtCheck', 'detekt']
}

// TODO(#2183): Add task to remove files in `build` on clean.

// https://github.com/passsy/gradle-gitVersioner-plugin
apply plugin: "com.pascalwelsch.gitversioner"
gitVersioner {
    baseBranch "master"
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
// To check which dependencies are out of date:
// ./gradlew dependencyUpdates
tasks.dependencyUpdates {

    // Disallow release candidates as upgradable versions from stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }

    checkForGradleUpdate = true
    checkConstraints = true
    gradleReleaseChannel = "current"
    outputFormatter = "json,plain"
    outputDir = "ground/build/reports/dependencyUpdates"
}

ext {
    androidCompileSdk = 34
    androidMinSdk = 24
    androidTargetSdk = 30

    gmsMapsVersion = '18.1.0'
    jvmToolchainVersion = 17
}