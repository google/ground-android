/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file. Configuration specified here is inherited by all sub-projects/modules.

buildscript {
    ext {
        hiltVersion = "2.44"
        kotlinVersion = "1.6.20"
        navigationVersion = "2.4.1"
    }
    repositories {
        google()
        jcenter()
        mavenLocal()
        maven { url "https://maven.google.com" }
        maven { url "https://github.com/ben-manes/gradle-versions-plugin/raw/mvnrepo" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
//        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath 'com.google.gms:google-services:4.3.10'
        classpath "com.pascalwelsch.gitversioner:gitversioner:0.5.0"
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.5'

        // Performance Monitoring plugin: https://firebase.google.com/docs/perf-mon
        classpath 'com.google.firebase:perf-plugin:1.4.1'

        // Crashlytics plugin
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.0'

        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.36.0"
    id "com.github.spotbugs" version "4.7.2" apply false
    id "org.jetbrains.kotlin.android" version "$kotlinVersion" apply false
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlinVersion"
    id "com.ncorti.ktfmt.gradle" version "0.9.0"
    id "com.google.dagger.hilt.android" version "$hiltVersion" apply false
}

allprojects {

    repositories {
        google() // For github repos.
        mavenLocal()
        maven { url "https://maven.google.com" }
        maven { url "https://jitpack.io" }
        maven { url "https://repo.osgeo.org/repository/release/" } // GeoTools
        jcenter()
    }
}

subprojects {
    apply plugin: "com.ncorti.ktfmt.gradle" // Version should be inherited from parent

    // https://github.com/cortinico/ktfmt-gradle
    ktfmt {
        googleStyle()
    }
}

task checkCode(type: GradleBuild) {
    tasks = ['checkstyle', 'lintDebug', 'pmd', 'spotbugsDebug', 'ktfmtCheck']
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// https://github.com/passsy/gradle-gitVersioner-plugin
apply plugin: "com.pascalwelsch.gitversioner"
gitVersioner {
    baseBranch "master"
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
// To check which dependencies are out of date:
// ./gradlew dependencyUpdates
tasks.dependencyUpdates {

    // Disallow release candidates as upgradable versions from stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }

    checkForGradleUpdate = true
    checkConstraints = true
    gradleReleaseChannel = "current"
    outputFormatter = "json,plain"
    outputDir = "ground/build/reports/dependencyUpdates"
}

ext {
    androidCompileSdk = 33
    androidMinSdk = 19
    androidTargetSdk = 30

    gmsMapsVersion = '17.0.1'
}